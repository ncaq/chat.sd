apply plugin: 'java'

defaultTasks 'build', 'javadoc'

allprojects {
    apply plugin: 'java'
}

subprojects {
    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile "org.mockito:mockito-core:1.9.5"
    }

    apply plugin: 'pmd'
    pmd.ignoreFailures = true
}

project(':server') {
    apply plugin: 'application'
    dependencies {
        compile project(':util')
        compile files('/usr/share/sqlite-jdbc/lib/sqlite-jdbc.jar')
    }
    run {
        mainClassName = 'net.ncaq.chat.sd.server.Main'
        systemProperty "java.library.path", "/usr/lib/sqlite-jdbc"
    }
}

project(':client') {
    apply plugin: 'application'
    dependencies {
        compile project(':util')
        compile 'gnu.getopt:java-getopt:1.0.13'
        testCompile project(':server')
    }
    run {
        mainClassName = 'net.ncaq.chat.sd.client.Main'
        if(project.hasProperty('args')) {
            args project.args.split('\\s+')
        }
    }
    test {
        systemProperty "java.library.path", "/usr/lib/sqlite-jdbc"
    }
}

project(':util') {
}

javadoc {
    source subprojects*.sourceSets.main.allJava
    classpath = files(subprojects*.sourceSets.main.compileClasspath)
    options.encoding = 'UTF-8'
}
